// based on https://github.com/SpeckyYT/CoffeeFuck

interpreter = (
    instructions: @string,
    length: @number,
    bits: @number,
    timeout: @number,
    input: [@number],
) {
    start = $.time()
    let output = []
    let memory = []
    let loops = []
    let skip = 0
    let pos = 0
    let cell = 0
    let inputsIndex = 0

    size = 2^bits

    time = () => $.time() - start
    exceededTimeout = () => time() >= timeout

    spreadMemory = (){
        while cell >= memory.length {
            memory.push(0)
        }
        while cell < 0 {
            memory.unshift(0)
            cell++
        }
    }

    while pos < instructions.length && !exceededTimeout() {
        spreadMemory()

        match instructions[pos] {
            =='[':
                loops.push(pos) if memory[cell] != 0 else skip++,
            ==']':
                (pos = loops[loops.length-1]) if memory[cell] != 0 else
                (skip-- if skip > 0 else loops.pop()),
        }

        if skip != 0 {
            pos++
            continue
        }

        match instructions[pos] {
            =='>':
                cell++ if cell < length-1 || memory.length < length  else 0,
            =='<':
                cell-- if cell > 0 || memory.length < length else 0,
            =='+': (){
                memory[cell] = (memory[cell]+1) % size
            }(),
            =='-': (){
                memory[cell] = (memory[cell]-1) % size
            }(),
            =='.':
                output.push(memory[cell]),
            ==',': (){
                if input.length < inputsIndex {
                    memory[cell] = input[inputsIndex] % size
                    inputsIndex++
                }
            }(),
        }

        spreadMemory()

        pos++
    }

    return @brainfuck::{
        output: output,
        memory: memory,
        timeout: exceededTimeout(),
        time: time(),
    }
}

natively = (
    instructions: @string,
    bits: @number,
) {
    let depth = 0
    let output = ''

    size = 2^bits

    output += 'start = $.time()\n'
    output += 'let memory = []\n'
    output += 'let pointer = 0\n'
    output += 'let output = []\n'
    output += 'fix_memory = () { while pointer < 0 { memory.unshift(0); pointer++ }; while pointer >= memory.length { memory.push(0) } }\n'
    output += 'fix_memory()\n'

    indent = (extra: @number = 0) => " " * 2 * (extra+depth)

    for char in instructions.split('') {
        result = match char {
            =='[': (){
                return 'while memory[pointer] != 0 {'
            }(),
            ==']': (){
                if depth <= 0 { return '' }
                depth--
                return '}'
            }(),
            =='+': 'memory[pointer] = (memory[pointer]+1) % {}'.fmt(size),
            =='-': 'memory[pointer] = (memory[pointer]-1) % {}'.fmt(size),
            =='>': 'pointer++; fix_memory()',
            =='<': 'pointer--; fix_memory()',
            =='.': 'output.push(memory[pointer])',
            else: '',
        }

        if result.length > 0 {
            output += '{}{} // {}\n'.fmt([ indent(), result, char ])
        } else if !(char in '\t\r\n ') {
            output += '{}// {}\n'.fmt([ indent(), char ])
        }

        // end
        match char {
            =='[': depth++,
        }
    }

    while depth > 0 {
        depth--
        output += indent() + '}\n'
    }

    output += 'return @brainfuck::{ output: output, memory: memory, timeout: false, time: $.time() - start }\n'

    return output
}

type @brainfuck
impl @brainfuck {
    new: (
        instructions: @string = '',
        length: @number = 2^32-1,
        bits: @number = 8,
        timeout: @number = 10^309,
        input: [@number] = [],
        native: @bool = false,
    ) {
        if native {
            output = natively(
                instructions = instructions,
                bits = bits,
            )
            $.writefile('native.spwn', output)
            return import 'native.spwn'
        } else {
            return interpreter(
                instructions = instructions,
                length = length,
                bits = bits,
                timeout = timeout,
                input = input,
            )
        }
    },
}
